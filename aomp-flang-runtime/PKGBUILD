# Maintainer: Torsten Ke√üler <t dot kessler at posteo dot de>

pkgname=aomp-flang-runtime
pkgdesc='Runtime component for fortran in AOMP'
_pkgver=11.6-1
pkgver=11.6.1
pkgrel=1
arch=('x86_64')
url='https://github.com/ROCm-Developer-Tools/flang'
license=('custom:Apache 2.0 with LLVM Exception')
depends=(aomp-flang aomp-pgmath)
makedepends=(cmake python)
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/aomp-$_pkgver.tar.gz")
sha256sums=('f94e6f72428816fdcda0a99de1e8d3405388a7f30bab16f6a727cab56b2de489')
options=(!strip)
_dirname="$(basename "$url")-$(basename ${source[0]} .tar.gz)"

build() {
    CC=/opt/rocm/aomp/bin/clang \
    CXX=/opt/rocm/aomp/bin/clang++ \
    FC=/opt/rocm/aomp/bin/flang \
    cmake -Wno-dev -B build \
          -S "$_dirname" \
          -DCMAKE_INSTALL_PREFIX='/opt/rocm/aomp' \
          -DCMAKE_BUILD_TYPE=Release \
          -DLLVM_ENABLE_ASSERTIONS=ON \
          -DLLVM_CONFIG='/opt/rocm/aomp/bin/llvm-config' \
          -DLLVM_TARGETS_TO_BUILD='AMDGPU;X86' \
          -DLLVM_INSTALL_RUNTIME=ON \
          -DFLANG_BUILD_RUNTIME=ON \
          -DCMAKE_SHARED_LINKER_FLAGS='-Wl,--disable-new-dtags' \
          -DCMAKE_INSTALL_RPATH_USE_LINK_PATH=ON \
          -DCMAKE_INSTALL_RPATH='$ORIGIN:$ORIGIN/../lib:/opt/rocm/aomp/lib'
  
    make -C build
}

package() {
    DESTDIR="$pkgdir" make -C build install
    # Remove duplicate file (already included in aomp-flang)
    rm "$pkgdir/opt/rocm/aomp/include/omp_lib.h"
}
