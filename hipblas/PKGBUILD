# Maintainer: Markus NÃ¤ther <naether.markus@gmail.com>
pkgname=hipblas
pkgver=3.3.0
pkgrel=2
pkgdesc='Next generation BLAS implementation for ROCm platform'
arch=('x86_64')
url="https://github.com/ROCmSoftwarePlatform/hipBLAS"
license=('MIT')
depends=('rocblas' 'hip')
optdepends=('cuda: cuBLAS backend')
makedepends=('cmake' 'hcc')
source=("$url/archive/rocm-$pkgver.tar.gz"
        "hipblas_hsa.patch")
sha256sums=('7be4f69749fb0b8deeaf47615f2c1ffbeee15f9cd6ef14cfea2a550cb0347a50'
            'd5206ce084f065f860ba1b1f9dc860c1500d9d4cc0b92473e1072ad819e8148d')

prepare() {
  cd "$srcdir/hipBLAS-rocm-$pkgver"
  patch -Np1 -i "$srcdir/hipblas_hsa.patch"
}

build() {
  mkdir -p build
  cd build

  # fix broken build with stack protection
  export CFLAGS="$(sed -e 's/-fstack-protector-strong//' <<< "$CFLAGS")"
  export CXXFLAGS="$(sed -e 's/-fstack-protector-strong//' <<< "$CXXFLAGS")"
  export CPPFLAGS="$(sed -e 's/-fstack-protector-strong//' <<< "$CPPFLAGS")"

  #TODO: libhipblas.so contains references to $srcdir
  CXX=/opt/rocm/hcc/bin/hcc \
  cmake -DCMAKE_INSTALL_PREFIX=/opt/rocm/hipblas \
        -Dhip_DIR=/opt/rocm/hip/lib/cmake/hip \
        -Dhcc_DIR=/opt/rocm/hcc/lib/cmake/hcc \
        -Damd_comgr_DIR=/opt/rocm/lib/cmake/amd_comgr \
        -DBUILD_CLIENTS_SAMPLES=OFF \
        -DBUILD_CLIENTS_TESTS=OFF \
        "$srcdir/hipBLAS-rocm-$pkgver"
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir" install

  install -Dm644 /dev/stdin "$pkgdir/etc/ld.so.conf.d/hipblas.conf" <<EOF
/opt/rocm/hipblas/lib
EOF
  install -Dm644 "$srcdir/hipBLAS-rocm-$pkgver/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
