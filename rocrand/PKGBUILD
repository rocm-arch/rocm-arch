# Maintainer: Jakub Oko≈Ñski <jakub@okonski.org>
pkgname=rocrand
pkgver=3.3.0
pkgrel=1
pkgdesc='Pseudo-random and quasi-random number generator on ROCm'
arch=('x86_64')
url="https://github.com/ROCmSoftwarePlatform/rocRAND"
license=('MIT')
depends=('hip')
makedepends=('hcc' 'cmake')
source=("$url/archive/rocm-$pkgver.tar.gz")
sha256sums=('ba56556671313b784a1301634df6537f3148426b81bec93b3566e71d22b6f4cc')

build() {
  mkdir -p build
  cd build

  # build broken with stack protection
  export CFLAGS="$(sed -e 's/-fstack-protector-strong//' <<< "$CFLAGS")"
  export CXXFLAGS="$(sed -e 's/-fstack-protector-strong//' <<< "$CXXFLAGS")"
  export CPPFLAGS="$(sed -e 's/-fstack-protector-strong//' <<< "$CPPFLAGS")"

  # TODO: fix lib{roc,hip}rand.so, they contain references to $srcdir
  CXX=/opt/rocm/hcc/bin/hcc \
  cmake -DBUILD_TEST=OFF \
        "$srcdir/rocRAND-rocm-$pkgver"
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir" install

  install -Dm644 /dev/stdin "$pkgdir/etc/ld.so.conf.d/rocrand.conf" << EOF
/opt/rocm/hiprand/lib
/opt/rocm/rocrand/lib
EOF
  install -Dm644 "$srcdir/rocRAND-rocm-$pkgver/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
