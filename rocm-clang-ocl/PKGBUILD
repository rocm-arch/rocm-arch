# Maintainer: acxz <akashpatel2008 at yahoo dot com>
# Contributor: Lucas Magalh√£es <whoisroot@national.shitposting.agency>
pkgname=rocm-clang-ocl
pkgver=3.5.0
pkgrel=2
pkgdesc="OpenCL compilation with clang compiler."
arch=('x86_64')
url="https://github.com/RadeonOpenCompute/clang-ocl"
license=('unknown')
depends=('llvm-amdgpu' 'rocm-opencl-runtime')
makedepends=('cmake' 'rocm-cmake')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/rocm-$pkgver.tar.gz"
        'remove_hcc_path.patch')
sha256sums=('38c95fbd0ac3d11d9bd224ad333b68b9620dde502b8a8a9f3d96ba642901e8bb'
            '54c9264971e1ca4d6999c40f5aa649ff127ddaaaea97a233b85943095f0912a1')
_dirname="$(basename "$url")-$(basename ${source[0]} .tar.gz)"

prepare() {
    cd "$_dirname"
    patch -Np1 -i "$srcdir/remove_hcc_path.patch"
}

build() {
  cmake -Wno-dev -B build \
        -S "$_dirname" \
        -DCLANG_BIN=/opt/rocm/llvm/bin \
        -DBITCODE_DIR=/opt/rocm/lib \
        -DCMAKE_INSTALL_PREFIX=/opt/rocm

  make -C build
}

check() {
  make -C build check
}

package() {
  DESTDIR="$pkgdir" make -C build install
}
