# Maintainer: acxz <akashpatel2008@yahoo.com>
pkgname=rock-dkms-bin
pkgver=3.5
_pkgver=$pkgver-30
pkgrel=3
pkgdesc="Linux AMD GPU kernel driver from ROC in DKMS format."
arch=('any')
url="https://github.com/RadeonOpenCompute/ROCK-Kernel-Driver"
license=('GPL')
depends=('dkms' 'rock-dkms-firmware')
provides=('rock-dkms')
conflicts=('rock-dkms')
backup=('etc/modprobe.d/blacklist-radeon.conf')
options=('!strip' '!emptydirs')
source=("http://repo.radeon.com/rocm/apt/debian/pool/main/r/rock-dkms/rock-dkms_${_pkgver}_all.deb"
        "rock_compatibility.patch"::"https://patch-diff.githubusercontent.com/raw/RadeonOpenCompute/ROCK-Kernel-Driver/pull/95.patch")

sha256sums=('f12a2cc3786bda711413f28be06d5ca0a0d44a441cf824455b0262da595a4ece'
            'e2afe9cd25934ebe32d9fec06e534b173b999bb0caf102f9a2a204f8b5c08b86')

package() {
  cd "$srcdir"

  tar xf data.tar.xz -C "$pkgdir"

  filterdiff -i "*Makefile" rock_compatibility.patch > Makefile.patch
  filterdiff -i "*amdgpu_bios.c" rock_compatibility.patch > amdgpu_bios.c.patch

  cd $pkgdir/usr/src/amdgpu-${_pkgver}/amd
  patch --forward --strip=4 dkms/Makefile --input=${srcdir}/Makefile.patch
  patch --forward --strip=4 amdgpu/amdgpu_bios.c --input=${srcdir}/amdgpu_bios.c.patch

  install -Dm644 "$pkgdir/usr/share/doc/rock-dkms/copyright" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
