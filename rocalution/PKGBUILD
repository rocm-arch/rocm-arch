# Maintainer: Markus NÃ¤ther <naetherm@informatik.uni-freiburg.de>
pkgname=rocalution
pkgver=3.3.0
pkgrel=2
pkgdesc="Library for iterative sparse solvers for ROCm platform"
arch=('x86_64')
url="https://github.com/ROCmSoftwarePlatform/rocALUTION"
license=('MIT')
depends=('hip' 'rocsparse' 'rocblas')
makedepends=('cmake' 'hcc')
source=("$url/archive/rocm-$pkgver.tar.gz")
sha256sums=('2745ad513ad90a7fb3d9182037ec499c3bbd19b6c928b5c24e777392b2c9bd2d')

build() {
  mkdir -p build
  cd build

  # TODO: fix librocalution.so, it contains references to $srcdir
  CXX=/opt/rocm/hcc/bin/hcc \
  cmake -DCMAKE_INSTALL_PREFIX=/opt/rocm/rocalution \
        -ROCSPARSE_DIR=/opt/rocm/rocsparse/lib/cmake/rocsparse \
        -Dhip_DIR=/opt/rocm/hip/lib/cmake/hip \
        -Dhcc_DIR=/opt/rocm/hcc/lib/cmake/hcc \
        -Damd_comgr_DIR=/opt/rocm/lib/cmake/amd_comgr \
        "$srcdir/rocALUTION-rocm-$pkgver"
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir" install

  install -Dm644 /dev/stdin "$pkgdir/etc/ld.so.conf.d/rocalution.conf" << EOF
/opt/rocm/rocalution/lib
EOF
  install -Dm644 "$srcdir/rocALUTION-rocm-$pkgver/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
