# Maintainer: Ranieri Althoff <ranisalt+aur at gmail dot com>

_opencl_icd_loader_commit='978b4b3a29a3aebc86ce9315d5c5963e88722d03'

pkgname=rocm-opencl-runtime
pkgver=3.3.0
pkgrel=3
pkgdesc='Radeon Open Compute - OpenCL runtime'
arch=('x86_64')
url='https://github.com/RadeonOpenCompute'
license=('MIT')
depends=('hsakmt-roct' 'hsa-rocr' 'opencl-icd-loader')
makedepends=(mesa cmake git llvm-roc rocm-comgr)
provides=("$pkgname" 'opencl-driver')
source=(
    "rocm-opencl-runtime::git+https://github.com/RadeonOpenCompute/ROCm-OpenCL-Runtime#tag=roc-$pkgver"
    "rocm-cmake::git+https://github.com/RadeonOpenCompute/rocm-cmake#tag=rocm-3.3.0"
    "opencl-icd-loader::git+https://github.com/KhronosGroup/OpenCL-ICD-Loader#commit=$_opencl_icd_loader_commit"
    'install_vendor_file.patch'
)

sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'b83de5ea8ae889664ce2725f90c5db8c1c9e98839d75c7743b355d16435dccee')

prepare() {
    cd "$srcdir/rocm-opencl-runtime"
    patch -Np1 -i "$srcdir/install_vendor_file.patch"

    mkdir -p api/opencl/khronos
    mv "$srcdir/opencl-icd-loader" api/opencl/khronos/icd
}

build() {
    cd "$srcdir/rocm-opencl-runtime"
    mkdir -p build && cd build
    cmake -DCMAKE_INSTALL_PREFIX=/opt/rocm \
          -DCMAKE_INSTALL_SYSCONFDIR=/etc \
          -DCMAKE_MODULE_PATH="$srcdir/rocm-cmake/share/rocm/cmake" \
          -DCMAKE_PREFIX_PATH=/opt/rocm/lib/cmake \
          -DLLVM_DIR=/opt/rocm/lib/cmake/llvm \
          -DUSE_COMGR_LIBRARY=yes \
          ..
    make
}

package() {
    DESTDIR="$pkgdir/" make -C "$srcdir/rocm-opencl-runtime/build" install

    install -Dm644 /dev/stdin "$pkgdir/etc/ld.so.conf.d/rocm-opencl.conf" <<-EOF
		/opt/rocm/lib/x86_64
	EOF

    install -Dm644 "$srcdir/rocm-opencl-runtime/License" "$pkgdir/usr/share/licenses/rocm-opencl-runtime/LICENSE"
}
