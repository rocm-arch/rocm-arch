# Maintainer: acxz <akashpatel2008 at yahoo dot com>
pkgname=rocm-smi-lib
pkgver=3.5.0
pkgrel=1
pkgdesc='Interface to monitor and control applications on AMD GPUs.'
arch=('x86_64')
url="https://github.com/RadeonOpenCompute/rocm_smi_lib"
license=('custom:NCSAOSL')
makedepends=('cmake')
options=(!staticlibs strip)
source=("rocm_smi_lib-roc-$pkgver.tar.gz::https://github.com/RadeonOpenCompute/rocm_smi_lib/archive/rocm-$pkgver.tar.gz")
sha256sums=('a5d2ec3570d018b60524f0e589c4917f03d26578443f94bde27a170c7bb21e6e')
_dirname="$(basename $url)-$(basename "${source[0]}" ".tar.gz")"

build() {
  cmake -B build -Wno-dev \
        -S "$_dirname" \
        -DCMAKE_INSTALL_PREFIX=/opt/rocm \
        -DCMAKE_BUILD_TYPE=Release 
  make -C build
}

package() {
  DESTDIR="$pkgdir" make -C build install

  install -Dm644 "$_dirname/License.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
