# Maintainer: Markus NÃ¤ther <naetherm@informatik.uni-freiburg.de>
pkgname=hipsparse
pkgver=3.3.0
pkgrel=2
pkgdesc="rocSPARSE marshalling library."
arch=('x86_64')
url="https://github.com/ROCmSoftwarePlatform/hipSPARSE"
license=('MIT')
depends=('rocsparse' 'hip')
optdepends=('cuda: cuSPARSE backend')
makedepends=('cmake' 'hcc')
source=("$url/archive/rocm-$pkgver.tar.gz"
        "hipsparse_hsa.patch")
sha256sums=('c69336071f56c857e969f0fdfbc351f75cc44ed2e3b854b4688675a9cafe4e22'
            'd3a926958b84c660f273d6b3a644e8a775e9ed779a7d81baa8f4c4981cee076b')

prepare() {
  cd "$srcdir/hipSPARSE-rocm-$pkgver"
  patch -Np1 -i "$srcdir/hipsparse_hsa.patch"
}

build() {
  mkdir -p build
  cd build

  # fix broken build with stack protection
  export CFLAGS="$(sed -e 's/-fstack-protector-strong//' <<< "$CFLAGS")"
  export CXXFLAGS="$(sed -e 's/-fstack-protector-strong//' <<< "$CXXFLAGS")"
  export CPPFLAGS="$(sed -e 's/-fstack-protector-strong//' <<< "$CPPFLAGS")"

  # TODO: fix librocsparse.so, it contains references to $srcdir
  CXX=/opt/rocm/hcc/bin/hcc \
  cmake -DCMAKE_INSTALL_PREFIX=opt/rocm/hipsparse \
        -Dhcc_DIR=/opt/rocm/hcc/lib/cmake/hcc \
        -Dhip_DIR=/opt/rocm/hip/lib/cmake/hip \
        -Damd_comgr_DIR=/opt/rocm/lib/cmake/amd_comgr \
        -Drocsparse_DIR=/opt/rocm/rocsparse/lib/cmake/rocsparse \
        -DBUILD_CLIENTS_SAMPLES=OFF \
        -DBUILD_CLIENTS_TESTS=OFF \
        -DHSA_HEADER=/opt/rocm/hsa/include \
        "$srcdir/hipSPARSE-rocm-$pkgver"
  make
}

package() {
  cd "$srcdir/build"

  make install

  cp -r "$srcdir/build/opt" "$pkgdir/"

  install -Dm644 /dev/stdin "$pkgdir/etc/ld.so.conf.d/hipsparse.conf" << EOF 
/opt/rocm/hipsparse/lib
EOF
  install -Dm644 "$srcdir/hipSPARSE-rocm-$pkgver/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
