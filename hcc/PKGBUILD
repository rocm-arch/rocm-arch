# Submitter: Olaf Leidinger <oleid@mescharet.de>
# Maintainer: Jakub Okoński <jakub@okonski.org>
# Maintainer: Rigo Reddig <rigo.reddig@gmail.com> ;)
# Maintainer: Markus Näther <naetherm@cs.uni-freiburg.de>
pkgname=hcc
pkgver=3.1.0
pkgrel=2
pkgdesc="HCC is an Open Source, Optimizing C++ Compiler for Heterogeneous Compute"
arch=('x86_64')
url="https://github.com/RadeonOpenCompute/hcc"
license=('custom:NCSAOSL')
depends=("hsa-rocr>=$pkgver" 'z3')
makedepends=('cmake' 'python')
options=(!staticlibs strip)
source=("hcc-roc-hcc-$pkgver.tar.gz::https://github.com/RadeonOpenCompute/hcc/archive/roc-hcc-$pkgver.tar.gz"
        "llvm-project-roc-hcc-$pkgver.tar.gz::https://github.com/RadeonOpenCompute/llvm-project/archive/roc-hcc-$pkgver.tar.gz"
        "ROCm-Device-Libs-roc-hcc-$pkgver.tar.gz::https://github.com/RadeonOpenCompute/ROCm-Device-Libs/archive/roc-hcc-$pkgver.tar.gz"
        "Fix-sanitizer-common-build-with-glibc-2.31.patch::https://github.com/llvm/llvm-project/commit/947f9692440836dcb8d88b74b69dd379d85974ce.patch")
sha256sums=('c9274230aa282e6ae90be5885e3f5b71b02153ca60980fc2122d61a5870bf2a2'
            'f76648116a2771a520545b9b5b3c931a8eb391f6f711f232924d92e8d1b43fc1'
            '42bd4f910dd44d6f0dd7bab7ebdadcf2e7cd4449ed7160dacc4471039584402c'
            'e08bc5ef423cb0df19df907dcfee84bf12a8a4211c7ea49afee33b70212fd40b')

prepare() {
  cd "$srcdir"
  mv -T "llvm-project-roc-hcc-$pkgver" "hcc-roc-hcc-$pkgver/llvm-project"
  mv -T "ROCm-Device-Libs-roc-hcc-$pkgver" "hcc-roc-hcc-$pkgver/rocdl"

  cd "$srcdir/hcc-roc-hcc-$pkgver/llvm-project"
  patch -Np1 -i "$srcdir/Fix-sanitizer-common-build-with-glibc-2.31.patch"
}

build() {
  mkdir -p "$srcdir/build"
  cd "$srcdir/build"

  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/opt/rocm/hcc \
        -DLLVM_INSTALL_UTILS=TRUE \
        "$srcdir/hcc-roc-hcc-$pkgver"
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir" install

  # add links
  install -d "$pkgdir/usr/bin"
  local _fn
  for _fn in hcc hcc-config; do
    ln -s "/opt/rocm/hcc/bin/$_fn" "$pkgdir/usr/bin/$_fn"
  done

  # additional link to make hcc demos happy
  install -d "$pkgdir/opt/rocm/include"
  ln -s /opt/rocm/hcc/include "$pkgdir/opt/rocm/include/hcc"

  # additional link to make clang-ocl work
  ln -s /opt/rocm/hcc/bin "$pkgdir/opt/rocm/hcc/bin/x86_64"
}
